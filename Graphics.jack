class Graphics{
    //draw a hollow circle by the specified parameters
    function void drawO ( int x, int y, int r, int w, boolean c) {
        var boolean prevColor;
        let prevColor = WindowController.getColor();
        do Screen.setColor(c);
        do Screen.drawCircle(x+r,y+r,r);
        do Screen.setColor(~c);
        do Screen.drawCircle(x+r,y+r,r-w);
        do WindowController.setColor(prevColor);
        return;
    }

    //draw an 'X' by the specified parameters
    function void drawX(int x, int y, int s, int w, boolean c){
        do Graphics.drawLine(x,y,x+s,y+s,w,c);
        do Graphics.drawLine(x,y+s,x+s,y,w,c);
        return;
    }
    
    //draw a line bfrom (x1,y1) to (x2,y2) with width of w anc color of c
    function void drawLine(int x1, int y1, int x2, int y2, int w, boolean c){
        var int i;
        let i=0;
        do Screen.setColor(c);
        while(i<w){
            do Screen.drawLine(x1+i-(w/2),y1+i-(w/2),x2+i-(w/2),y2+i-(w/2));
            let i=i+1;
        }
        return;
    }

    //approximatly draws an int in the correct location with moveCursor
    function void drawInt(int x, int y,int v){
        var int r,c;
        let r = WindowController.getRow();
        let c =WindowController.getCol();
        do WindowController.moveCursor(y*22/254,x*63/510);
        do Output.printInt(v);
        do WindowController.moveCursor(r,c);
        return;
    }

     //approximatly draws a string in the correct location with moveCursor
    function void drawString(int x, int y,String s){
        var int r,c;
        let r = WindowController.getRow();
        let c = WindowController.getCol();
        do WindowController.moveCursor(y*22/254,x*63/510);
        do Output.printString(s);
        do WindowController.moveCursor(r,c);
        return;
    }

    // draws a grid with top left at (x,y) with total size of 's' where each division is of size 'd' and each line is of width 'w' and color 'c'
    function void drawG(int x, int y, int s, int d, int w, boolean c){
        var int i,p;
        let p=s/d;
        let i=0;
        do Graphics.drawInt(x+(i*p)+(p/2),y,i);
        do Graphics.drawInt(x,y+(i*p)+(p/2),i);
        let i=1;
        while (i<d){
            do Graphics.drawLine(x+(i*p),y,x+(i*p),y+s,w,c);
            do Graphics.drawInt(x+(i*p)+(p/2)+(i*2),y,i);
            do Graphics.drawLine(x,y+(i*p),x+s,y+(i*p),w,c);
            do Graphics.drawInt(x,y+(i*(p+(254/22)))+(p/2)-(i*5),i);
            let i=i+1;
        }
        return;
    }

    //clears the screen and resets the cursor
    function void clear(){
        do Screen.clearScreen();
        do WindowController.moveCursor(0,0);
        return;
    }
}